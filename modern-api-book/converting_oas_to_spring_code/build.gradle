plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'org.hidetake.swagger.generator' version '2.19.2'
}

group = 'com.pakt'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// https://mvnrepository.com/artifact/org.openapitools/openapi-generator-cli
	compileOnly 'org.openapitools:openapi-generator-cli:7.9.0'
	// https://mvnrepository.com/artifact/io.swagger.core.v3/swagger-annotations
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.25'

	// https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-validation
	implementation 'org.springframework.boot:spring-boot-starter-validation:3.3.5'

	// https://mvnrepository.com/artifact/org.openapitools/jackson-databind-nullable
	implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

	// https://mvnrepository.com/artifact/com.fasterxml.jackson.dataformat/jackson-dataformat-xml
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'

	// https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-hateoas
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'





}

tasks.named('test') {
	useJUnitPlatform()
}

swaggerSources {
	def typeMappings = 'URI=URI'	
	def importMappings = 'URI=java.net.URI'

	eStore {
		def apiYaml = "${rootDir}/src/main/resources/api/openapi.yml"
		def configJson = "${rootDir}/src/main/resources/api/config.json"
		inputFile = file(apiYaml)
		def ignoreFile = file("${rootDir}/src/main/resources/api/.openapi-generator-ignore")

		code {
			language = 'spring'
			configFile = file(configJson)
			rawOptions = ['--ignore-file-override', ignoreFile, '--type-mappings', typeMappings, '--import-mappings', importMappings] as List<String>
			components = [models: true, apis: true, supportingFiles: 'ApiUtil.java']
		}
	}
}

compileJava.dependsOn swaggerSources.eStore.code
sourceSets.main.java.srcDir "${swaggerSources.eStore.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.eStore.code.outputDir}/src/main/resources"